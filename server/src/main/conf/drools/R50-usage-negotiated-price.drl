package br.com.auster.rules.vivo.R50;

import java.util.List;
import java.util.Map;
import java.util.Set;
import java.util.HashSet;
import java.util.Map.Entry;
import java.util.Iterator;
import java.util.HashMap;
import br.com.auster.om.pricing.TaxRate;

import java.util.concurrent.atomic.AtomicLong;
import br.com.auster.common.rules.GlobalForLog;
import br.com.auster.common.lang.NamedHashMap;
import br.com.auster.common.lang.NamedDouble;

import br.com.auster.om.invoice.Account;
import br.com.auster.om.invoice.Invoice;
import br.com.auster.om.invoice.ServiceDetail;
import br.com.auster.om.invoice.SectionDetail;
import br.com.auster.om.invoice.UsageDetail;
import br.com.auster.om.invoice.VoiceUsageDetail;
import br.com.auster.om.invoice.DataUsageDetail;
import br.com.auster.om.invoice.EventUsageDetail;
import br.com.auster.om.invoice.FreeUnitsDetail;
import br.com.auster.om.pricing.ServicePrice;
import br.com.auster.om.pricing.ServiceFreeUnits;

import br.com.auster.om.util.UnitConvertor;

import br.com.auster.vivo.billcheckout.util.FreeUnitsBucket;
import br.com.auster.vivo.billcheckout.util.ServiceNotFoundSet;
import br.com.auster.vivo.billcheckout.util.CheckList;
import br.com.auster.vivo.billcheckout.util.ChecklistClasses;
import br.com.auster.vivo.billcheckout.util.DimensionCache
import br.com.auster.vivo.billcheckout.util.InvoiceUtils;

import br.com.auster.billcheckout.consequence.telco.TelcoConsequenceBuilder;
import br.com.auster.billcheckout.consequence.Consequence;
import br.com.auster.billcheckout.consequence.ConsequenceAttributeList.AttributeType;
import br.com.auster.billcheckout.consequence.telco.TelcoConsequence;
import br.com.auster.billcheckout.consequence.telco.AccountDimension;
import br.com.auster.billcheckout.consequence.telco.GeographicDimension;
import br.com.auster.billcheckout.consequence.telco.TimeDimension;
import br.com.auster.billcheckout.consequence.telco.CarrierDimension;
import br.com.auster.billcheckout.consequence.telco.CycleDimension;

import br.com.auster.common.text.ValueFormat;
import br.com.auster.common.text.DurationFormat;

global br.com.auster.vivo.billcheckout.util.DimensionCache dimensionCache;
global br.com.auster.billcheckout.consequence.telco.TelcoConsequenceBuilder consequenceBuilder;
global java.util.List results;

function void buildR50Consequence01(List results, DimensionCache dimensionCache, TelcoConsequenceBuilder consequenceBuilder, 
		 String ruleNbr, String desc, String carrierCode, UsageDetail usg) {
    consequenceBuilder.setRule(ruleNbr, "Tarifação de Uso Negociado");
    consequenceBuilder.setAccount((AccountDimension) dimensionCache.getFromCache("account"));
    consequenceBuilder.setGeographics((GeographicDimension) dimensionCache.getFromCache("geo"));		
    consequenceBuilder.setTime((TimeDimension) dimensionCache.getFromCache("time"));		
    consequenceBuilder.setCycle((CycleDimension) dimensionCache.getFromCache("cycle"));	
    consequenceBuilder.setCarrier((CarrierDimension) dimensionCache.getFromCache(carrierCode));

    TelcoConsequence c = (TelcoConsequence) consequenceBuilder.getConsequence();
    c.setDescription(desc);	
    c.addAttribute("Ciclo", usg.getInvoice().getCycleCode());
    c.addAttribute("NomeDoServico", usg.getSvcPlan());
	c.addAttribute("Valor Calculado", ValueFormat.formatValue(usg.getCalculatedValue()));                              	        
	c.addAttribute("Valor Cobrado", ValueFormat.formatValue(usg.getTotalAmount()));
	c.addAttribute("Diferença", ValueFormat.formatValue((usg.getCalculatedValue() - usg.getTotalAmount())));
	c.addAttribute("Classe Chamada",usg.getCallClass());
	//c.addAttribute("Sub Classe Chamada", usg.getCallSubclass());
	c.addAttribute("Tarifa Chamada", usg.getTariffClass());
	c.addDateAttribute("Data", usg.getDatetime(), "dd/MM/yy");
	c.addAttribute("Hora", DurationFormat.formatFromDate(usg.getDatetime()));
	c.addAttribute("Número Origem", usg.getChannelId());
	c.addAttribute("Número Destino", ValueFormat.formatPhone(usg.getCalledNumber()));
	c.addAttribute("Duração Chamada", usg.getUsageDuration());
	c.addAttribute("Cenário", usg.getSvcId());
	c.addAttribute("Valor Inicial", ValueFormat.formatValue(usg.getPrice().getInitCost()));
	c.addAttribute("Valor Adicional", ValueFormat.formatValue(usg.getPrice().getStepCost()));
	c.addAttribute("Unidade", usg.getPrice().getInitType());
	c.addAttribute("Categoria", usg.getPrice().getTaxCategory().getTaxCode() );
	
	String subState = usg.getSection().getParentSection().getParentSection().getSubscriptionState();
	TaxRate taxRate = usg.getPrice().getTaxCategory().getTaxRate(subState,usg.getDatetime());			
	double usedTaxRate = (taxRate == null)? -1 : taxRate.getTaxRate();
	c.addAttribute("Imposto", ValueFormat.formatValue(usedTaxRate));	
	c.addAttribute("Estado Subscrição", subState);
	c.addAttribute("Tipo de Cliente", usg.getInvoice().getAccount().getAccountType());
    results.add(c); 
}

 
rule "SERVICE_INIT_FOR_R50"
	salience 100
	when
		not ( NamedHashMap( name == "NegotRatingErrorLimit" ) )
	then		
		NamedDouble limit = new NamedDouble("NegotRatingErrorLimit");	 
		limit.setValue(2);
		insert(limit);
		
		NamedHashMap currentErrors = new NamedHashMap("NegotCurrentRatingErrors");
		insert(currentErrors);
end

/**
 * RULE #50. - Valor de chamadas nacionais (menos recebidas em Roaming)
 *
 * Rerates all voice usage details.
 *
 * If a problem is found, report it and also, if there is a scenario for 
 * that voice usage, increment the error count.
 */
rule "R50-1"
	salience 10
	when	
		$usage : VoiceUsageDetail (
			priceNegotiated == true,
			passThrough == false,
	#		callClass != ChecklistClasses.CALL_CLASS_AD1,
			$carrierCode : carrierCode,
			rated == true,
			objectType != "A",
			$tag : tag ,
			$priceInfo : price -> ($priceInfo != null),
			$amtDiff : amountDifference -> ($amtDiff >= 0.015)
	    ) 
	    

	   $lim : NamedDouble( name == "NegotRatingErrorLimit" )
	    
	   $err : NamedHashMap( name == "NegotCurrentRatingErrors" )
	then
		$usage.setRated(SectionDetail.WRONGLY_RATED);
		String svc = $usage.getSvcPlan();
	
		HashMap svcMap = (HashMap) $err.get(svc);
		if (svcMap == null) {
			svcMap = new HashMap();
			$err.put(svc, svcMap);
		}
		String keyForCounter = $usage.getCallClass() + "." + $usage.getCallSubclass() + "." + $usage.getTariffClass();
		AtomicLong counter = (AtomicLong) svcMap.get(keyForCounter);
		if (counter == null) {
			counter = new AtomicLong(0);
		}
		
		svcMap.put(keyForCounter, counter);
		counter.incrementAndGet(); 
						
		if (counter.get() <= $lim.getValue() ) {
			buildR50Consequence01(results, dimensionCache, consequenceBuilder, "R50-01", 
			                      "Chamada Nacional Negociada Tarifada Incorretamente", $carrierCode, $usage);
        }
end

/**
 * RULE #1.4  valor de SMS/MMS
 */
rule "R50-2"
	salience 10
	when
		$usage : EventUsageDetail (
			priceNegotiated == true,		
			passThrough == false,
	#		callClass != ChecklistClasses.CALL_CLASS_AD1,
			$carrierCode : carrierCode,
			rated == true,
			objectType != "A",
			$tag : tag,
			$priceInfo : price -> ($priceInfo != null),
			$amtDiff : amountDifference -> ($amtDiff >= 0.015)
	    ) 

	    $lim : NamedDouble( name == "NegotRatingErrorLimit" )
	    
	    $err : NamedHashMap( name == "NegotCurrentRatingErrors" )
	then
		$usage.setRated(SectionDetail.WRONGLY_RATED);
		
		String svc = $usage.getSvcPlan();
		HashMap svcMap = (HashMap) $err.get(svc);
		if (svcMap == null) {
			svcMap = new HashMap();
			$err.put(svc, svcMap);
		} 			
		String keyForCounter = $usage.getCallClass() + "." + $usage.getCallSubclass() + "." + $usage.getTariffClass();
		AtomicLong counter = (AtomicLong) svcMap.get(keyForCounter);
		if (counter == null) {
			counter = new AtomicLong(0);
		}

		svcMap.put(keyForCounter, counter);
   		counter.incrementAndGet();		
   				
		if (counter.get() <= $lim.getValue()) {
			buildR50Consequence01(results, dimensionCache, consequenceBuilder, "R50-02", 
									"Evento de Mensagem Negociado Tarifado Incorretamente", $carrierCode, $usage);
        }	
end

/**
 * RULE #1.8: valor de servicos de dados
 */
rule "R50-3"
	salience 10
	when
		$usage : DataUsageDetail (
			priceNegotiated == true,				
			passThrough == false,
		#	callClass != ChecklistClasses.CALL_CLASS_AD1,
			$carrierCode : carrierCode,
			rated == true,
			objectType != "A",
			$tag : tag,
			$priceInfo : price -> ($priceInfo != null),
			$amtDiff : amountDifference -> ($amtDiff >= 0.015)
	    ) 

	    $lim : NamedDouble( name == "NegotRatingErrorLimit" )
	    
	    $err : NamedHashMap( name == "NegotCurrentRatingErrors" )		
	then
		$usage.setRated(SectionDetail.WRONGLY_RATED);	
		
		String svc = $usage.getSvcPlan();
		HashMap svcMap = (HashMap) $err.get(svc);
		if (svcMap == null) {
			svcMap = new HashMap();
			$err.put(svc, svcMap);
		} 			
		String keyForCounter = $usage.getCallClass() + "." + $usage.getCallSubclass() + "." + $usage.getTariffClass();
		AtomicLong counter = (AtomicLong) svcMap.get(keyForCounter);
		if (counter == null) {
			counter = new AtomicLong(0);
		}

		svcMap.put(keyForCounter, counter);
  		counter.incrementAndGet(); 	
  						
		if (counter.get() <= $lim.getValue()) {
			buildR50Consequence01(results, dimensionCache, consequenceBuilder, "R50-03", "Chamada de Dados Negociada Tarifada Incorretamente", $carrierCode, $usage);
        }
end     

/**
 * RULE #00-3: Quantidade de eventos tarifados incorretamente
 */
rule "R50 - Wrong Rating Counter Dump"
	salience 0
	when
		$inv : Invoice ( )
	    $lim : NamedDouble( name == "NegotRatingErrorLimit" )
	    
	    $err : NamedHashMap( name == "NegotCurrentRatingErrors" ) 
	then	
	  Iterator itr;
		for (itr=$err.entrySet().iterator();itr.hasNext();) {
			Map.Entry entry = (Map.Entry) itr.next();
			String p1 = (String) entry.getKey();
			Map svcMap = (Map) entry.getValue();
			Iterator itSVC;
			for (itSVC=svcMap.entrySet().iterator(); itSVC.hasNext();) {
				Map.Entry countEntry = (Map.Entry) itSVC.next();
				//String p2 = p1 + "." + (String) countEntry.getKey();
				String p2 = (String) countEntry.getKey();
				AtomicLong counter = (AtomicLong) countEntry.getValue();
				//Just create consequence for this type, if the number of errors is greater than the limit.				
				if (counter.get() > $lim.getValue()) {
					consequenceBuilder.setRule("A00-1","Contador de Tarifação Incorreta"); 
					consequenceBuilder.setAccount((AccountDimension) dimensionCache.getFromCache("account"));
					consequenceBuilder.setGeographics((GeographicDimension) dimensionCache.getFromCache("geo"));
					consequenceBuilder.setTime((TimeDimension) dimensionCache.getFromCache("time"));
					consequenceBuilder.setCycle((CycleDimension) dimensionCache.getFromCache("cycle"));
					consequenceBuilder.setCarrier((CarrierDimension) dimensionCache.getFromCache($inv.getAccount().getCarrierCode()));
					TelcoConsequence c = (TelcoConsequence) consequenceBuilder.getConsequence();
					c.setDescription("Preço Negociado com Excesso de Erros de Tarifação.");	
					c.addLongAttribute("Ocorrencias",counter.get(), "0");
					c.addAttribute("NomeDoServico", p1); 
					c.addAttribute("Qualificacao", p2); 
					results.add(c);  
				}
			}
		}
end		    
